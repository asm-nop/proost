WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
number = @{ ASCII_DIGIT+ }
filename = @{ !keywords ~ ( ASCII_ALPHANUMERIC | PUNCTUATION )+ }
wildcard = @{"_"}
string = @{!keywords ~ ASCII_ALPHA ~ ( "_" | ASCII_ALPHANUMERIC )* }
keywords = @{ ( "fun" | "def" | "check" | "eval" | "search" | "import" | "Prop" | "Type" | "Sort" ) ~ !ASCII_ALPHANUMERIC }
eoi = _{ !ANY }


simple_term = _{ VarDecl | Var | Prop | Type | Sort | "(" ~ Term ~ ")" }
Term = _{ Abs | dProd | Prod | App | simple_term }
term_prod = _{ App | Abs | dProd | simple_term }
term_app = _{ Abs | simple_term } 


arg = { (wildcard | string)+ ~ ":" ~ Term }
arg_par = _{ arg | "(" ~ arg_par ~ ")" }

Args = { ("(" ~ arg_par ~ ")")* }
ArgsParOpt = _{ ("(" ~ arg_par ~ ")")+ | arg_par }

Abs = { ( "fun" ~ ArgsParOpt ~ "=>" ~ Term ) }
dProd = { "(" ~ arg_par ~ ")" ~ "->" ~ Term }
App = { term_app ~ term_app+ }
Prod = { term_prod ~ ( "->" ~ term_prod )+ }

Prop = { "Prop" }
Type = { "Type" ~ univ? }
Sort = { "Sort" ~ univ? }
univ = _{ Plus | univ_plus }
univ_plus = _{ Max | IMax | Num | string | "(" ~ univ ~ ")" }
Num = { number }
Plus = { univ_plus ~ ( "+" ~ number )+ }
Max = { ( "max" ~ "(" ~ univ ~ "," ~ univ ~ ")" ) | ( "max" ~ univ ~ univ ) }
IMax = { ( "imax" ~ "(" ~ univ ~ "," ~ univ ~ ")" ) | ( "imax" ~ univ ~ univ ) }

Var = { string }
VarDecl = ${ string ~ arg_univ }
stringDecl = ${ string ~ univ_decl }

arg_univ = !{".{" ~ (univ ~ ("," ~ univ)* )? ~ "}"}
univ_decl = !{".{" ~ (string ~ ("," ~ string)* )? ~ "}"}


Command = _{ Define | Declaration | DeclarationCheckType | CheckType | GetType | DefineCheckType | Eval | ImportFile | Search}
Define = { "def" ~ string ~ Args ~ ":=" ~ Term }
DefineCheckType = { "def" ~ string ~ Args ~ ":" ~ Term ~ ":=" ~ Term }
Declaration = { "def" ~ stringDecl ~ Args ~ ":=" ~ Term }
DeclarationCheckType = { "def" ~ stringDecl ~ Args ~ ":" ~ Term ~ ":=" ~ Term }
CheckType = { "check" ~ Term ~ ":" ~ Term }
GetType = { "check" ~ Term }
Eval = { "eval" ~ Term }
ImportFile = { "import" ~ filename* }
Search = { "search" ~ string }

command = _{SOI ~ Command ~ eoi }
file = _{ SOI ~ Command* ~ eoi }
